#+TITLE: ~/.zshrc
* History
Set up the history file.
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  HISTFILE=~/.histfile
  HISTSIZE=1000
  SAVEHIST=1000
  # save each command's beginning timestamp and the duration to the history file
  setopt extended_history

  zstyle :compinstall filename '/home/kevin/.zshrc'

  # display PID when suspending processes as well
  setopt longlistjobs
#+END_SRC

* Completion
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  autoload -Uz compinit
  compinit

  # in order to use #, ~ and ^ for filename generation grep word
  # *~(*.gz|*.bz|*.bz2|*.zip|*.Z) -> searches for word not in compressed files
  # don't forget to quote '^', '~' and '#'!
  setopt extended_glob

  # autocompletion of command line switches for aliases
  setopt COMPLETE_ALIASES
#+END_SRC

Autocompletion of privileged environments in privileged commands, e.g. sudo
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  zstyle ':completion::complete:*' gain-privileges 1
  zstyle ':completion:*' menu select
#+END_SRC

some things from zsh-lovers https://grml.org/zsh/zsh-lovers.html

Fuzzy matching of completions for when you mistype them:
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  zstyle ':completion:*' completer _complete _match _approximate
  zstyle ':completion:*:match:*' original only
  zstyle ':completion:*:approximate:*' max-errors 1 numeric
#+END_SRC

And if you want the number of errors allowed by _approximate to increase with the length of what you have typed so far:
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  zstyle -e ':completion:*:approximate:*' \
  max-errors 'reply=($((($#PREFIX+$#SUFFIX)/3))numeric)'
#+END_SRC

Ignore completion functions for commands you don’t have:
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  zstyle ':completion:*:functions' ignored-patterns '_*'
#+END_SRC

With helper functions like:
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  xdvi() { command xdvi ${*:-*.dvi(om[1])} }
#+END_SRC

you can avoid having to complete at all in many cases, but if you do, you might want to fall into menu selection immediately and to have the words sorted by time:
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  zstyle ':completion:*:*:xdvi:*' menu yes select
  zstyle ':completion:*:*:xdvi:*' file-sort time
#+END_SRC

* Paths
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  export PATH="$HOME/bin:$HOME/.local/bin:/usr/local/bin:$HOME/.node_modules/bin:$(ruby -e 'print Gem.user_dir')/bin:$home/composer/vendor/bin:$PATH"
  export npm_config_prefix=~/.node_modules
#+END_SRC

* Plugins
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  # autosuggestions
  # installed with pacman
  source /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh

  # syntax highlighting
  # installed with pacman
  source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

  # history search
  # installed with pacman
  source /usr/share/zsh/plugins/zsh-history-substring-search/zsh-history-substring-search.zsh
#+END_SRC
* Prompt
Custom prompt with snippets of code taken from various places.

The initial setup.
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  autoload -Uz promptinit vcs_info
  promptinit
#+END_SRC

If set, parameter expansion, command substitution and arithmetic expansion are performed in prompts. Substitutions within prompts do not affect the command status.
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  setopt prompt_subst
#+END_SRC

Set up how git is displayed.
- based off https://stackoverflow.com/a/12935606
  - green dot is staged
  - yellow dot is unstaged
  - red dot is untracked
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  zstyle ':vcs_info:*' stagedstr '✚'
  zstyle ':vcs_info:*' unstagedstr '●'
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' actionformats '%F{5}[%F{4}%b%F{3}|%F{1}%a%F{5}]%f '
  zstyle ':vcs_info:*' formats \
    '%F{8}[%F{7}±%B%b%%b %F{2}%c%F{3}%u%F{8}]%f' # %%b is bold off
  zstyle ':vcs_info:git*+set-message:*' hooks git-untracked
  zstyle ':vcs_info:*' enable git 
  +vi-git-untracked() {
    if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]] && \
    [[ $(git ls-files --other --directory --exclude-standard | sed q | wc -l | tr -d ' ') == 1 ]] ; then
    hook_com[unstaged]+='%F{1}●%f'
  fi
  }

  precmd() {
      vcs_info
  }
#+END_SRC

Set up the actual prompt.
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  # from robbyrussel
  # show a green error if the exit code is 0, otherwise show a red arrow
  ret_status="%(?:%F{green}λ%f:%F{red}λ%f)"
  newline=$'\n'

  PROMPT='${newline}%B%F{cyan}%6~%f ${ret_status}%b '
  RPROMPT='${vcs_info_msg_0_}'
#+END_SRC

* Aliases
Pacman aliases
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  alias pac='sudo pacman'
  alias pacs='pac -S'
  alias pacu='pac -Syu'
  alias pacy='pac -Sy'
  alias pacss='pac -Ss'
  alias pacq='pac -Qs'
#+END_SRC

List aliases
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  alias ll='ls -l'
  alias lla='ls -la'
#+END_SRC

Git aliases
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  alias g='git'
  alias gs='git status'
  alias gss='git status -s'
#+END_SRC

* Fixes
** Delete Key
The delete key by default inserts a ~, this makes it act like it should
#+BEGIN_SRC sh :tangle ~/dotfiles/zsh/.zshrc
  bindkey "^[[3~" delete-char
#+END_SRC
